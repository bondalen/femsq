VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Cias"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Compare Database

' объект ~Факт связи тройки (СФ+Д)+СГК+ОПСД~. 24.03.2022

' ###################################################################################################################################################
' область определения закрытых пременных, содержащих значения свойств ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' !!! не нужно их писать в других местах, это приведет к ошибке. 07.07.2022
' ключ объекта ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
Private mlngCiasKey As Long
' база данных
Private mdbDb As DAO.Database
' область определения закрытых пременных, содержащих значения свойств ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' ---------------------------------------------------------------------------------------------------------------------------------------------------
Const cstrTable As String = "ags_cnInvAccntSmpl", cstrTableKeyField As String = "ciasKey"
' ---------------------------------------------------------------------------------------------------------------------------------------------------
' ###################################################################################################################################################

' ###################################################################################################################################################
' геттеры и сеттеры (леттеры) свойств

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство ключ объекта ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства ключ объекта ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
Public Property Get ciasKey() As Long
    ciasKey = mlngCiasKey
End Property
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство *родительский объект ~Связь договора и счёта-фактуры~*. 07.02.2023
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства *родительский объект ~Связь договора и счёта-фактуры~*
Public Property Get cnInvParentObj() As cnInv
    
    Dim strSql As String, rs As DAO.Recordset

    ' -----------------------------------------------------------------------------------------------------------------------------------------------
    If IsNull(mlngCiasKey) = False And Not mdbDb Is Nothing Then
        If mlngCiasKey > 0 Then
            
            strSql = "SELECT ciasCnInv FROM " & cstrTable & " WHERE (((" & cstrTableKeyField & ")=" & mlngCiasKey & "));"
            Set rs = mdbDb.OpenRecordset(strSql, dbOpenSnapshot)
            If rs.RecordCount > 0 Then
                Set cnInvParentObj = ClassFactory.cnInvReadKey(rs!ciasCnInv, mdbDb)
            End If
            rs.Close: Set rs = Nothing
        Else
            Set cnInvParentObj = Nothing
        End If
    Else
        Set cnInvParentObj = Nothing
    End If
End Property ' ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' свойство *родительский объект ~Связь договора и счёта-фактуры~*. 07.02.2023. Окончание
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство *родительский объект ~Счёт Главной книги~*. 07.02.2023
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства *родительский объект ~Счёт Главной книги~*
Public Property Get accntParentObj() As Accnt
    
    Dim strSql As String, rs As DAO.Recordset

    ' -----------------------------------------------------------------------------------------------------------------------------------------------
    If IsNull(mlngCiasKey) = False And Not mdbDb Is Nothing Then
        If mlngCiasKey > 0 Then
            
            strSql = "SELECT ciasAccnt FROM " & cstrTable & " WHERE (((" & cstrTableKeyField & ")=" & mlngCiasKey & "));"
            Set rs = mdbDb.OpenRecordset(strSql, dbOpenSnapshot)
            If rs.RecordCount > 0 Then
                Set accntParentObj = ClassFactory.accntByKey(rs!ciasAccnt, mdbDb)
            End If
            rs.Close: Set rs = Nothing
        Else
            Set accntParentObj = Nothing
        End If
    Else
        Set accntParentObj = Nothing
    End If
End Property ' ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' свойство *родительский объект ~Счёт Главной книги~*. 07.02.2023. Окончание
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' геттеры и сеттеры (леттеры) свойств. Окончание
' ###################################################################################################################################################

' ###################################################################################################################################################
' инициализации и реинициализации
' ***************************************************************************************************************************************************
' обработка встроенного события инициализации
Private Sub Class_Initialize()
    ' инициализируем объект с нулевым ключём. В таком виде он не имеет смысла.
    ' Требуется реинициализация.
    ' -1 означает, что проверки наличия свойства не было. Если позже будет установлено значение свойства,
    ' то оно будет приведено к 0 при отсутствии и к значению при наличии.
    ' Это чтобы избежать необходимости каждый раз вычислять свойство заново
    mlngCiasKey = -1
End Sub
' ***************************************************************************************************************************************************

' ***************************************************************************************************************************************************
' реинициализируем объект *Факт связи тройки (СФ+Д)+СГК+ОПСД* по известному ключу такого объекта. 07.02.2023
' ...................................................................................................................................................
Public Sub ReinitByKey(ByVal ciasKey As Long, ByRef boResult As Boolean, ByRef db As DAO.Database)
    ' ...............................................................................................................................................
    Dim rs As DAO.Recordset, strSql As String
    ' -----------------------------------------------------------------------------------------------------------------------------------------------
    Const cstrTitle As String = "Процедура *Реинициализируем объект *Факт связи тройки (СФ+Д)+СГК+ОПСД* по известному ключу такого объекта*."
' ---------------------------------------------------------------------------------------------------------------------------------------------------
On Error GoTo ErrHandler

    ' ключ объекта равен -1? То есть реинициализация ранее не проводилась
    If mlngCiasKey = -1 Then
        ' да,ключ объекта равен -1
        ' отыскиваем объект по известному ключу
        strSql = "SELECT * FROM " & cstrTable & " WHERE " & cstrTableKeyField & " = " & ciasKey & ";"
        Set rs = db.OpenRecordset(strSql, dbOpenSnapshot)
        
        ' имеются ли записи с указанным ключём?
        If rs.RecordCount = 1 Then
            ' да, записи с указанным ключём имеются
            mlngCiasKey = ciasKey
            Set mdbDb = db ' устанавливаем ссылку на базу данных
            boResult = True ' результат положительный
        Else
            ' нет, записи с указанным ключём отсутствуют
            mlngCiasKey = 0
            boResult = False ' результат отрицательный
        End If
    Else
        ' да,ключ объекта не равен -1
        ' ключ объекта равен 0?
        If mlngCiasKey = 0 Then
            ' да, ключ объекта равен 0. Ранее он безуспешно реинициализировался
            boResult = False
        Else
            ' нет, ключ объекта не равен 0. Ранее он успешно реинициализировался
            boResult = True
        End If
    End If
    
    ' закрываем набор записей
    rs.Close: Set rs = Nothing
    
NormalExit:
    Exit Sub
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle: Resume NormalExit
End Sub
' реинициализируем объект *Факт связи тройки (СФ+Д)+СГК+ОПСД* по известному ключу такого объекта. 07.02.2023. Окончание
' ***************************************************************************************************************************************************

' ***************************************************************************************************************************************************
' реинициализируем объект ~Факт связи тройки (СФ+Д)+СГК+ОПСД~ через создание нового такого объекта по СФ+Д, СГК и ОПСД. 24.03.2022 ******************
Public Sub riCiasCreateNew(ciasCnInv As Long, ciasAccnt As Long, ciasCsos As Long, db As DAO.Database, strNote As String)

    Dim rsCias As DAO.Recordset
    
    Const cstrTitle As String _
        = "Процедура *Реинициализируем объект ~Факт связи тройки (СФ+Д)+СГК+ОПСД~ через создание нового такого объекта по СФ+Д, СГК и ОПСД*."
        
    '************************************************************************************************************************************************
    
On Error GoTo ErrHandler
    
    ' создаём объект
    ' открываем таблицу для создания
    Set rsCias = db.OpenRecordset("ags_cnInvAccntSmpl", dbOpenDynaset, dbSeeChanges)
    
    ' добавляем объект
    With rsCias
        .AddNew
            !ciasCnInv = ciasCnInv
            !ciasAccnt = ciasAccnt
            !ciasCn_s_org_smpl = ciasCsos
            !ciasNote = strNote
            !ciasTimeOfEntry = Now()
        .Update
        .Bookmark = .LastModified
        mlngCiasKey = !ciasKey
        Set mdbDb = db ' устанавливаем ссылку на базу данных
    End With
    
    ' закрываем таблицу
    rsCias.Close: Set rsCias = Nothing
    
NormalExit:
    Exit Sub
    
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
    
End Sub
' реинициализируем объекта ~Факт связи тройки (СФ+Д)+СГК+ОПСД~ через создание нового такого объекта по СФ+Д, СГК и ОПСД. 24.03.2022. Окончание ******
' ***************************************************************************************************************************************************
' инициализации и реинициализации. Окончание
' ###################################################################################################################################################

