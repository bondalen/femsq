VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ra_a"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Compare Database

' объект *Ревизия отчётов агента*

' ###################################################################################################################################################
' область определения закрытых пременных, содержащих значения свойств ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' !!! не нужно их писать в других местах, это приведет к ошибке. 07.07.2022
' ключ объекта *Ревизия отчётов агента*
Private mlngRa_aKey As Long

' дочерний объект *Ревизия отчётов агента. Проверка файла Отчётов всех агентов*
Private mRa_aAllAgentsChild As ra_aAllAgents

' дочерний объект *Ревизия ОА. Проверка файла Актов агентского вознаграждения*
Private mRa_aAgFee23_06Child As ra_aAgFee23_06

' объект поля результатов ревизии
Private mobgRa_aResult As TextBox

' создавать отсутствующие отчёты пр наличии строек?
Private mblnAdt_AddRA As Boolean
' область определения закрытых пременных, содержащих значения свойств. Окончание
' ###################################################################################################################################################

' ###################################################################################################################################################
' геттеры и сеттеры (леттеры) свойств
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство *Объект поля результатов ревизии*. ??.??.????
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' сеттер свойства объект поля результатов ревизии
Public Property Set obgRa_aResult(ByRef ra_aResult As TextBox)
    If mobgRa_aResult Is Nothing Then
        Set mobgRa_aResult = ra_aResult
    End If
End Property ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства объект поля результатов ревизии
Public Property Get obgRa_aResult() As TextBox
    Set obgRa_aResult = mobgRa_aResult
End Property ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' свойство *Объект поля результатов ревизии*. ??.??.????. Окончание
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' геттер свойства ключ объекта *Ревизия отчётов агента*
Public Property Get adt_key() As Long
    adt_key = mlngRa_aKey
End Property

' геттер свойства *создавать отсутствующие отчёты пр наличии строек?*
Public Property Get blnAdt_AddRA() As Boolean
    blnAdt_AddRA = mblnAdt_AddRA
End Property

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство дочерний объект *Ревизия отчётов агента. Проверка файла Отчётов всех агентов*. ??.??.????
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства дочерний объект *Ревизия отчётов агента. Проверка файла Отчётов всех агентов*
Public Property Get ra_aAllAgentsChild() As ra_aAllAgents
    If Not mRa_aAllAgentsChild Is Nothing Then
        If mRa_aAllAgentsChild.Ra_aAllAgentsKey = -1 Or mRa_aAllAgentsChild.Ra_aAllAgentsKey = 0 Then
            Set ra_aAllAgentsChild = Nothing
        Else
            Set ra_aAllAgentsChild = mRa_aAllAgentsChild
        End If
    Else
        Set ra_aAllAgentsChild = Nothing
    End If
End Property ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' сеттер свойства дочерний объект *Ревизия отчётов агента. Проверка файла Отчётов всех агентов*. 06.07.2022
' вернее это не сеттер в стандартном понимании, а процедура. Но, для понятности, её удобнее разместить здесь.
Public Sub ra_aAllAgentsChildAdd(ByVal Ra_aAllAgentsKey As Long, db As DAO.Database)

    'Dim rsRa_a As DAO.Recordset, strSql As String
    
    Dim ra_aAllAgentsNew As ra_aAllAgents, setNecessary As Boolean
    
    Const cstrTitle As String _
        = "Сеттер свойства дочерний объект *Ревизия отчётов агента. Проверка файла Отчётов всех агентов*."
        
    '************************************************************************************************************************************************
    
On Error GoTo ErrHandler

    setNecessary = False
    
    If mRa_aAllAgentsChild Is Nothing Then
        setNecessary = True
    Else
        If mRa_aAllAgentsChild.Ra_aAllAgentsKey <> Ra_aAllAgentsKey Then
            setNecessary = True
        End If
    End If

    If setNecessary Then
        ' создаём новый объект класса *Ревизия отчётов агента. Проверка файла Отчётов всех агентов* по известному ключу
        Set ra_aAllAgentsNew = ClassFactory.ra_aAllAgentsReadKey(Ra_aAllAgentsKey, Me, db)
        ' имеется ли объект с указанным ключём?
        If Not ra_aAllAgentsNew Is Nothing Then
            Set mRa_aAllAgentsChild = ra_aAllAgentsNew
        End If
    End If

NormalExit:
    Exit Sub
    
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
    
End Sub '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' свойство дочерний объект *Ревизия отчётов агента. Проверка файла Отчётов всех агентов*. ??.??.????. Окончание
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство дочерний объект *Ревизия ОА. Проверка файла Актов агентского вознаграждения*. 29.06.2023
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства дочерний объект *Ревизия ОА. Проверка файла Актов агентского вознаграждения*
Public Property Get ra_aAgFee23_06Child() As ra_aAgFee23_06
    If Not mRa_aAgFee23_06Child Is Nothing Then
        If mRa_aAgFee23_06Child.Ra_aAgFee23_06Key = -1 Or mRa_aAgFee23_06Child.Ra_aAgFee23_06Key = 0 Then
            Set ra_aAgFee23_06Child = Nothing
        Else
            Set ra_aAgFee23_06Child = mRa_aAgFee23_06Child
        End If
    Else
        Set ra_aAgFee23_06Child = Nothing
    End If
End Property ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' сеттер свойства дочерний объект *Ревизия ОА. Проверка файла Актов агентского вознаграждения*.
' вернее это не сеттер в стандартном понимании, а процедура. Но, для понятности, её удобнее разместить здесь.
Public Sub ra_aAgFee23_06ChildAdd(ByVal Ra_aAgFee23_06Key As Long, db As DAO.Database)
    ' ...............................................................................................................................................
    Dim ra_aAgFee23_06New As ra_aAgFee23_06, setNecessary As Boolean
    ' -----------------------------------------------------------------------------------------------------------------------------------------------
    Const cstrTitle As String _
        = "Сеттер свойства дочерний объект *Ревизия ОА. Проверка файла Актов агентского вознаграждения*."
' ---------------------------------------------------------------------------------------------------------------------------------------------------
    
On Error GoTo ErrHandler

    setNecessary = False
    
    If mRa_aAgFee23_06Child Is Nothing Then
        setNecessary = True
    Else
        If mRa_aAgFee23_06Child.Ra_aAgFee23_06Key <> Ra_aAgFee23_06Key Then
            setNecessary = True
        End If
    End If

    If setNecessary Then
        ' создаём новый объект класса *Ревизия ОА. Проверка файла Актов агентского вознаграждения* по известному ключу
        Set ra_aAgFee23_06New = ClassFactory.ra_aAgFee23_06ByKey(Ra_aAgFee23_06Key, Me, db)
        ' создался ли объект с указанным ключём?
        If Not ra_aAgFee23_06New Is Nothing Then
            ' да, объект с указанным ключём создался
            ' присваиваем ссылку на вновь созданный объект свойству
            Set mRa_aAgFee23_06Child = ra_aAgFee23_06New
        End If
    End If

NormalExit:
    Exit Sub
    
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
    
End Sub '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' свойство дочерний объект *Ревизия ОА. Проверка файла Актов агентского вознаграждения*. 29.06.2023. Окончание
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' геттер свойства год объекта *Ревизия отчётов агента*
Public Property Get year(db As DAO.Database) As Integer

    Dim rsRa_a As DAO.Recordset, strSql As String, adt_dir As Long

    Const cstrTitle As String _
        = "Процедура *Геттер свойства год объекта ~Ревизия отчётов агента~*."
        
    '************************************************************************************************************************************************
    
On Error GoTo ErrHandler

    ' имеется ли ключ объекта ревизии?
    If Not mlngRa_aKey = 0 Then
        ' да, ключ объекта ревизии имеется
        ' получаем ключ директории проверки
        strSql = "SELECT r.adt_key, r.adt_dir FROM ra_a AS r WHERE (((r.adt_key)=" & mlngRa_aKey & "));"
        Set rsRa_a = db.OpenRecordset(strSql, dbOpenSnapshot)
            rsRa_a.MoveFirst
            adt_dir = rsRa_a!adt_dir
        rsRa_a.Close: Set rsRa_a = Nothing
        ' получаем ключ первого отчётного периода директории
        strSql = "SELECT s.rdsp_period, s.rdsp_dir, y.yyyy " _
            & "FROM (ra_dir_s_p AS s INNER JOIN ags_ra_period AS p ON s.rdsp_period = p.key) INNER JOIN ags_yyyy AS y ON p.y = y.yKey " _
            & "WHERE (((s.rdsp_dir)=" & adt_dir & "));"
        Set rsRa_a = db.OpenRecordset(strSql, dbOpenSnapshot)
            ' имеются ли периоды?
            If rsRa_a.RecordCount > 0 Then
                ' да, периоды имеются
                rsRa_a.MoveFirst
                year = rsRa_a!yyyy
            Else
                ' нет, периоды отсутствуют
                year = 0
            End If
        rsRa_a.Close: Set rsRa_a = Nothing
    Else
        ' нет, ключ объекта ревизии отсутствует
        year = 0
    End If
    
NormalExit:
    Exit Property
    
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
    
End Property ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' геттеры и сеттеры (леттеры) свойств. Окончание
' ###################################################################################################################################################

' обработка встроенного события инициализации
Private Sub Class_Initialize()
    ' инициализируем объект с нулевым ключём. В таком виде он не имеет смысла.
    ' Требуется реинициализация.
    ' -1 означает, что проверки наличия свойства не было. Если позже будет установлено значение свойства,
    ' то оно будет приведено к 0 при отсутствии и к значению при наличии.
    ' Это чтобы избежать необходимости каждый раз вычислять свойство заново
    mlngRa_aKey = -1
    ' Set mRa_aAllAgentsChild = Nothing
End Sub

' реинициализируем объект *Ревизия отчётов агента* по известному ключу такого объекта. 04.04.2022 ***************************************************
Public Sub riRa_aReadKey(ByVal ra_a As Long, ByRef boResult As Boolean, db As DAO.Database)

    Dim rsRa_a As DAO.Recordset, strSql As String
    
    Const cstrTitle As String _
        = "Процедура *Реинициализируем объект *Ревизия отчётов агента* по известному ключу такого объекта*."
        
    '************************************************************************************************************************************************
    
On Error GoTo ErrHandler

    ' ключ объекта равен -1? То есть реинициализация ранее не проводилась
    If mlngRa_aKey = -1 Then
        ' да,ключ объекта равен -1
        ' отыскиваем объект счёта-фактуры по известному ключу
        strSql = "SELECT r.adt_key, r.adt_AddRA FROM ra_a AS r WHERE (((r.adt_key)=" & ra_a & "));"
        Set rsRa_a = db.OpenRecordset(strSql, dbOpenSnapshot)
        
        ' имеются ли записи с указанным ключём?
        If rsRa_a.RecordCount = 1 Then
            ' да, записи с указанным ключём имеются
            mlngRa_aKey = ra_a
            mblnAdt_AddRA = rsRa_a!adt_AddRA
            boResult = True
        Else
            ' нет, записи с указанным ключём отсутствуют
            mlngRa_aKey = 0
            boResult = False
        End If
    Else
        ' да,ключ объекта не равен -1
        ' ключ объекта равен 0?
        If mlngRa_aKey = 0 Then
            ' да, ключ объекта равен 0. Ранее он безуспешно реинициализировался
            boResult = False
        Else
            ' нет, ключ объекта не равен 0. Ранее он успешно реинициализировался
            boResult = True
        End If
    End If
    
    ' закрываем набор записей
    rsRa_a.Close: Set rsRa_a = Nothing
    
NormalExit:
    Exit Sub
    
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
    
End Sub
' реинициализируем объект *Ревизия отчётов агента* по известному ключу такого объекта. 04.04.2022. Окончание ****************************************

