VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "CnInv"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Compare Database

' объект ~Связь договора и счёта-фактуры~

' ###################################################################################################################################################
' область определения закрытых пременных, содержащих значения свойств ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' !!! не нужно их писать в других местах, это приведет к ошибке. 07.07.2022
' ключ связи договора и счёта-фактуры
Private lngCnInvKey As Long
' база данных
Private mdbDb As DAO.Database
' дочерний объект коллекции объектов ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
Private mcolCias As CnInvColCias
' ---------------------------------------------------------------------------------------------------------------------------------------------------
Const cstrTable As String = "ags_cnInv", cstrTableKeyField As String = "ciKey"
' ---------------------------------------------------------------------------------------------------------------------------------------------------
' ###################################################################################################################################################

' ###################################################################################################################################################
' геттеры и сеттеры (леттеры) свойств

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' геттер свойства дочернего объекта коллекции объектов ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
Public Property Get colCias() As CnInvColCias
    ' возвращаем указатель на mcolCias, дочерний объект коллекции объектов ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
    Set colCias = mcolCias
End Property
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' геттер свойства ключ связи договора и счёта-фактуры
Public Property Get ciKey() As Long
    ciKey = lngCnInvKey
End Property
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство *родительский объект ~Счёт-фактура~*. 07.02.2023
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства *родительский объект ~Счёт-фактура~*
Public Property Get invParentObj() As Inv
    
    Dim strSql As String, rs As DAO.Recordset

    ' -----------------------------------------------------------------------------------------------------------------------------------------------
    If IsNull(lngCnInvKey) = False And Not mdbDb Is Nothing Then
        If lngCnInvKey > 0 Then
            
            strSql = "SELECT ciInv FROM " & cstrTable & " WHERE (((" & cstrTableKeyField & ")=" & lngCnInvKey & "));"
            Set rs = mdbDb.OpenRecordset(strSql, dbOpenSnapshot)
            If rs.RecordCount > 0 Then
                Set invParentObj = ClassFactory.invByKey(rs!ciInv, mdbDb)
            End If
            rs.Close: Set rs = Nothing
        Else
            Set invParentObj = Nothing
        End If
    Else
        Set invParentObj = Nothing
    End If
End Property ' ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' свойство *родительский объект ~Счёт-фактура~*. 07.02.2023. Окончание
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' свойство *родительский объект ~Договор~*. 07.02.2023
' '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' геттер свойства *родительский объект ~Счёт-фактура~*
Public Property Get cnParentObj() As Cn
    
    Dim strSql As String, rs As DAO.Recordset

    ' -----------------------------------------------------------------------------------------------------------------------------------------------
    If IsNull(lngCnInvKey) = False And Not mdbDb Is Nothing Then
        If lngCnInvKey > 0 Then
            
            strSql = "SELECT ciCn FROM " & cstrTable & " WHERE (((" & cstrTableKeyField & ")=" & lngCnInvKey & "));"
            Set rs = mdbDb.OpenRecordset(strSql, dbOpenSnapshot)
            If rs.RecordCount > 0 Then
                Set cnParentObj = ClassFactory.cnByKey(rs!ciCn, mdbDb)
            End If
            rs.Close: Set rs = Nothing
        Else
            Set cnParentObj = Nothing
        End If
    Else
        Set cnParentObj = Nothing
    End If
End Property ' ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' свойство *родительский объект ~Договор~*. 07.02.2023. Окончание
' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
' геттеры и сеттеры (леттеры) свойств. Окончание
' ###################################################################################################################################################

' ###################################################################################################################################################
' инициализации и реинициализации, завершение
' ***************************************************************************************************************************************************
' обработка встроенного события инициализации
Private Sub Class_Initialize()
    ' инициализируем объект с нулевым ключём связи договора и счёта-фактуры. В таком виде он не имеет смысла.
    ' Требуется реинициализация.
    lngCnInvKey = 0
    
    ' создаём дочерний класс коллекции объектов ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
    Set mcolCias = New CnInvColCias
    Set mcolCias.parent = Me
End Sub

' обработка встроенного события завершения
Private Sub Class_Terminate()
    ' ликвидируем дочерний класс коллекции объектов ~Факт связи тройки (СФ+Д)+СГК+ОПСД~
    Set mcolCias = Nothing
End Sub

'****************************************************************************************************************************************************
' реинициализируем объект связи договора и счёта-фактуры через создание новой такой связи для нового
' счёта-фактуры и известного ключа договора
Public Sub riCnInvCreateNewInvNew(invNum As Variant, invDate As Variant, cnKey As Long, db As DAO.Database, strNote As String)
    Dim invNew As Inv, rsCnInv As DAO.Recordset
    Const cstrTitle As String _
        = "Процедура *Реинициализируем объект связи договора и счёта-фактуры через создание новой такой связи*."
    '************************************************************************************************************************************************
On Error GoTo ErrHandler

    ' создаём объект нового счёта-фактуры по номеру и дате
    Set invNew = ClassFactory.invCreateNewNumDate(invNum, invDate, db, strNote)
    
    ' создаём объект связи
    ' открываем таблицу для создания
    Set rsCnInv = db.OpenRecordset("ags_cnInv", dbOpenDynaset, dbSeeChanges)
    
    ' добавляем связь счёта-фактуры и договора
    With rsCnInv
        .AddNew
            !ciInv = invNew.iKey
            !ciCn = cnKey
            !ciNote = strNote
            !ciTimeOfEntry = Now()
        .Update
        .Bookmark = .LastModified
        lngCnInvKey = !ciKey
        Set mdbDb = db ' устанавливаем ссылку на базу данных
    End With
    
    ' закрываем таблицу
    rsCnInv.Close: Set rsCnInv = Nothing
    
    Set invNew = Nothing
    
NormalExit:
    Exit Sub
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
End Sub
'****************************************************************************************************************************************************

'****************************************************************************************************************************************************
' реинициализируем объект связи договора и счёта-фактуры по известным ключу счёта-фактуры и ключу договора. 25.10.2022 ******************************
Public Sub riCnInvCreateNewInvCn(InvKey As Long, cnKey As Long, db As DAO.Database, strNote As String)
    Dim rsCnInv As DAO.Recordset
    Const cstrTitle As String = "Процедура *Реинициализируем объект связи договора и счёта-фактуры по известным ключу счёта-фактуры и ключу договора*."
    '************************************************************************************************************************************************
On Error GoTo ErrHandler

    ' создаём объект связи
    ' открываем таблицу для создания
    Set rsCnInv = db.OpenRecordset("ags_cnInv", dbOpenDynaset, dbSeeChanges)
    
    ' добавляем связь счёта-фактуры и договора
    With rsCnInv
        .AddNew
            !ciInv = InvKey
            !ciCn = cnKey
            !ciNote = strNote
            !ciTimeOfEntry = Now()
        .Update
        .Bookmark = .LastModified
        lngCnInvKey = !ciKey
        Set mdbDb = db ' устанавливаем ссылку на базу данных
    End With
    
    ' закрываем таблицу
    rsCnInv.Close: Set rsCnInv = Nothing

NormalExit:
    Exit Sub
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
End Sub
' реинициализируем объект связи договора и счёта-фактуры по известным ключу счёта-фактуры и ключу договора. 25.10.2022. Окончание *******************
'****************************************************************************************************************************************************

' реинициализируем объект связи договора и счёта-фактуры по известному ключу такого объекта. 23.03.2022 *********************************************
Public Sub riCnInvReadKey(ByVal cnInv As Long, ByRef boResult As Boolean, db As DAO.Database)

    Dim rsCnInv As DAO.Recordset, strSql As String
    
    Const cstrTitle As String _
        = "Процедура *Реинициализируем ~объект связи договора и нового счёта-фактуры~ по известному ключу такого объекта*."
        
    '************************************************************************************************************************************************
    
On Error GoTo ErrHandler

    ' отыскиваем объект счёта-фактуры по известному ключу
    strSql = "SELECT ci.ciKey FROM ags_cnInv AS ci WHERE (((ci.ciKey)=" & cnInv & "));"
    Set rsCnInv = db.OpenRecordset(strSql, dbOpenSnapshot)
    
    ' имеются ли записи с указанным ключём?
    If rsCnInv.RecordCount = 1 Then
        ' да, записи с указанным ключём имеются
        lngCnInvKey = cnInv
        Set mdbDb = db ' устанавливаем ссылку на базу данных
        boResult = True
        Else
        ' нет, записи с указанным ключём отсутствуют
        boResult = False
    End If
    
    ' закрываем набор записей
    rsCnInv.Close: Set rsCnInv = Nothing
    
NormalExit:
    Exit Sub
    
ErrHandler:
    MsgBox Err.Description & vbCrLf & "Error number: " & Err.Number & vbCrLf & cstrTitle, vbExclamation, cstrTitle
    Resume NormalExit
    
End Sub
' реинициализируем объект связи договора и счёта-фактуры по известному ключу такого объекта. 23.03.2022. Окончание
' **********************************
' инициализации и реинициализации. Окончание
' ###################################################################################################################################################




